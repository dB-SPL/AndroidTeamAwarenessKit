buildscript { scriptHandler ->
    apply from: 'gradle/repositories.gradle', to: scriptHandler
    apply from: 'gradle/versions.gradle', to: project

    dependencies {
        // Only required for Android builds, but doesn't hurt to declare it for all builds
        classpath "com.android.tools.build:gradle:$androidToolsBuildGradleVersion"
    }
}

// library version

def majorVersion = 0
def minorVersion = 0
def patchVersion = 0

// Version code for Android builds, you MUST increase this by 1 if you change ANY of the version components above
ext.versionCode = 1

apply from: 'gradle/buildtype.gradle'

allprojects {
    group = 'gov.tak.kernel'
    version = "${majorVersion}.${minorVersion}.${patchVersion}"
}

subprojects { subProject ->
    apply from: configProject.file('gradle/buildconfig.gradle'), to: subProject

    subProject.afterEvaluate {
        apply from: configProject.file('gradle/publishing.gradle'), to: it
    }
}
